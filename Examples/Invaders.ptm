
TITLE "Invaders"

DEF enemy_layer,	0
DEF player_layer,	1
DEF hiscore_file,	"hiscore.dat"

#================================================
	init:
#================================================
VAR player_x,					20
VAR player_y,					22
VAR player_is_shooting,			0
VAR player_missile_x,			0
VAR player_missile_y,			0
VAR player_mv_ctr,				0
VAR player_mv_ctr_max,			5
VAR player_missile_mv_ctr,		0
VAR player_missile_mv_ctr_max,	3
VAR score, 0
VAR hiscore, 0

VAR enemy_mv_ctr,		0
VAR enemy_mv_ctr_max,	100

CALL init_charset
VOL 15
BGCOL &h90
TRON

FILE.CHK hiscore_file,has_hiscore
IF.EQ has_hiscore,1
	CLOAD hiscore_file,hiscore
ENDIF

#================================================
	gameplay_loop:
#================================================
LAYER player_layer
CLL
CALL draw_info
CALL draw_player
IF.EQ player_is_shooting,1
	CALL draw_player_missile
	CALL move_player_missile
	CALL check_player_missile_collision
ENDIF

IF.KEY RIGHT
	VAR player_dx,1
	CALL move_player
ENDIF
IF.KEY LEFT
	VAR player_dx,-1
	CALL move_player
ENDIF
IF.KEY SPACE
	CALL player_shoot
ENDIF
IF.KEY UP
	ADD enemy_mv_ctr,enemy_mv_ctr,10
ENDIF

INC enemy_mv_ctr
IF.GT enemy_mv_ctr,enemy_mv_ctr_max
	VAR enemy_mv_ctr,0
	CALL spawn_enemy_row
ENDIF

CALL check_for_defeat

VSYNC
GOTO gameplay_loop

#================================================
	check_for_defeat:
#================================================
VAR is_defeat,0
LAYER enemy_layer
FOR x,0,44
	LOCATE x,player_y
	GET
	TILE.GETP prop,"type"
	IF.EQS prop,"enemy"
		VAR is_defeat,1
	ENDIF
NEXT
IF.EQ is_defeat,1
	LAYER player_layer
	INK &h35
	LOCATE 16,23
	PRINT     "GAME  OVER"
	LOCATE 16,23
	PRINT.ADD "          "
	VSYNC
	PLAY "O2L80BAGFEDCCC"
	CSAVE hiscore_file,hiscore
	PAUSE 400
	RESET
ENDIF
RET

#================================================
	spawn_enemy_row:
#================================================
LAYER enemy_layer
MOVB 0,0,44,22,0,1
TILE.NEW ch_enemy_1,&hc6,0
TILE.ADD ch_enemy_2,&hc7,0
TILE.SETP "type","enemy"
FOR i,0,5
	RND x,10,33
	LOCATE x,0
	PUT
NEXT
RET

#================================================
	spawn_enemies:
#================================================
LAYER enemy_layer
TILE.NEW ch_enemy_1,&hc6,0
TILE.ADD ch_enemy_2,&hc7,0
TILE.SETP "type","enemy"
FOR i,0,200
	RND x,10,33
	RND y,0,20
	LOCATE x,y
	PUT
NEXT
RET

#================================================
	draw_info:
#================================================
LAYER player_layer

LOCATE 1,23
INK &h03
PRINT "score "
INK &h08
STR.FMT score_fmt,"%07i",score
PRINT score_fmt

LOCATE 28,23
INK &h03
PRINT "hi-score "
INK &h08
STR.FMT hiscore_fmt,"%07i",hiscore
PRINT hiscore_fmt
RET

#================================================
	draw_player:
#================================================
LAYER player_layer
TILE.NEW ch_player,&h0c,0
LOCATE player_x,player_y
PUT
RET

#================================================
	move_player:
#================================================
IF.EQ player_mv_ctr,0
	ADD player_x,player_x,player_dx
	IF.LT player_x,0
		VAR player_x,0
	ENDIF
	IF.GT player_x,44
		VAR player_x,44
	ENDIF
ENDIF
INC player_mv_ctr
IF.GT player_mv_ctr,player_mv_ctr_max
	VAR player_mv_ctr,0
ENDIF
RET

#================================================
	player_shoot:
#================================================
IF.EQ player_is_shooting,1
	RET
ENDIF
PLAY "L10O4CDF"
VAR player_is_shooting,1
VAR player_missile_x,player_x
VAR player_missile_y,player_y
DEC player_missile_y
RET

#================================================
	draw_player_missile:
#================================================
LAYER player_layer
TILE.NEW ch_missile,&hfc,0
LOCATE player_missile_x,player_missile_y
PUT
RET

#================================================
	move_player_missile:
#================================================
IF.EQ player_missile_mv_ctr,0
	DEC player_missile_y
	IF.LT player_missile_y,0
		VAR player_is_shooting,0
	ENDIF
ENDIF
INC player_missile_mv_ctr
IF.GT player_missile_mv_ctr,player_missile_mv_ctr_max
	VAR player_missile_mv_ctr,0
ENDIF
RET

#================================================
	check_player_missile_collision:
#================================================
LAYER enemy_layer
LOCATE player_missile_x,player_missile_y
GET
TILE.GETP prop,"type"
IF.EQS prop,"enemy"
	TILE.NEW ch_enemy_dead,&h34,0
	TILE.SETP "type","enemy_dead"
	PUT
	VSYNC
	PLAY "L10O6ECD"
	VAR player_is_shooting,0
	ADD score,score,10
	IF.GT score,hiscore
		VAR hiscore,score
	ENDIF
ENDIF
RET

#================================================
	init_charset:
#================================================
DEF ch_player,1
CHR 1,0,&b00011000
CHR 1,1,&b00011000
CHR 1,2,&b00111100
CHR 1,3,&b01011010
CHR 1,4,&b11011011
CHR 1,5,&b11111111
CHR 1,6,&b00100100
CHR 1,7,&b00000000

DEF ch_enemy_1,2
CHR 2,0,&b00000000
CHR 2,1,&b00111000
CHR 2,2,&b01111100
CHR 2,3,&b10101010
CHR 2,4,&b01111100
CHR 2,5,&b10000010
CHR 2,6,&b01000100
CHR 2,7,&b00000000

DEF ch_enemy_2,3
CHR 3,0,&b00111000
CHR 3,1,&b01111100
CHR 3,2,&b11010110
CHR 3,3,&b01111100
CHR 3,4,&b10000010
CHR 3,5,&b10000010
CHR 3,6,&b00000000
CHR 3,7,&b00000000

DEF ch_missile,4
CHR 4,0,&b00011000
CHR 4,1,&b00011000
CHR 4,2,&b00011000
CHR 4,3,&b00000000
CHR 4,4,&b00011000
CHR 4,5,&b00000000
CHR 4,6,&b00011000
CHR 4,7,&b00000000

DEF ch_enemy_dead,5
CHR 5,0,&b00000000
CHR 5,1,&b00000000
CHR 5,2,&b01001000
CHR 5,3,&b00110000
CHR 5,4,&b00110000
CHR 5,5,&b01001000
CHR 5,6,&b00000000
CHR 5,7,&b00000000

RET
