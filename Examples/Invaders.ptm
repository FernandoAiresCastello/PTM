
TITLE "Invaders"

DEF bg_layer,        0
DEF enemy_layer,    1
DEF player_layer,    2

DEF hiscore_file,    "hiscore.dat"

#================================================
    init:
#================================================
VAR player_x,                    20
VAR player_y,                    22
VAR player_is_shooting,          0
VAR player_missile_x,            0
VAR player_missile_y,            0
VAR player_mv_ctr,               0
VAR player_mv_ctr_max,           5
VAR player_missile_mv_ctr,       0
VAR player_missile_mv_ctr_max,   3
VAR score, 0
VAR hiscore, 0

VAR enemy_mv_ctr,        0
VAR enemy_mv_ctr_max,    150

CALL init_palette
CALL init_charset
VOL 15
BGCOL &h00

LAYER bg_layer
TILE.NEW 0,0,col_blue1
RECT 0,0,45,5
TILE.NEW 0,0,col_blue2
RECT 0,5,45,8
TILE.NEW 0,0,col_blue3
RECT 0,13,45,10

TRON

FILE.CHK hiscore_file,has_hiscore
IF.EQ has_hiscore,1
    CLOAD hiscore_file,hiscore
ENDIF

#================================================
    gameplay_loop:
#================================================
LAYER player_layer
CLL
CALL draw_info
CALL draw_player
IF.EQ player_is_shooting,1
    CALL draw_player_missile
    CALL move_player_missile
    CALL check_player_missile_collision
ENDIF

IF.KEY "RIGHT"
    VAR player_dx,1
    CALL move_player
ENDIF
IF.KEY "LEFT"
    VAR player_dx,-1
    CALL move_player
ENDIF
IF.KEY "SPACE"
    CALL player_shoot
ENDIF
IF.KEY "UP"
    ADD enemy_mv_ctr,enemy_mv_ctr,10
ENDIF

INC enemy_mv_ctr
IF.GT enemy_mv_ctr,enemy_mv_ctr_max
    VAR enemy_mv_ctr,0
    CALL spawn_enemy_row
ENDIF

CALL check_for_defeat

VSYNC
GOTO gameplay_loop

#================================================
    check_for_defeat:
#================================================
VAR is_defeat,0
LAYER enemy_layer
FOR x,0,44
    LOCATE x,player_y
    GET
    TILE.GETP prop,"type"
    IF.EQ prop,"enemy"
        VAR is_defeat,1
    ENDIF
NEXT
IF.EQ is_defeat,1
    LAYER player_layer
    INK col_orange
    LOCATE 16,23
    PRINT     "GAME  OVER"
    LOCATE 16,23
    PRINT.ADD "          "
    VSYNC
    PLAY "O2L80BAGFEDCCC"
    CSAVE hiscore_file,hiscore
    PAUSE 400
    RESET
ENDIF
RET

#================================================
    spawn_enemy_row:
#================================================
LAYER enemy_layer
MOVB 0,0,44,22,0,1
TILE.NEW ch_enemy_1,col_green,0
TILE.ADD ch_enemy_2,col_green,0
TILE.SETP "type","enemy"
FOR i,0,10
    RND x,13,30
    LOCATE x,0
    PUT
NEXT
RET

#================================================
    draw_info:
#================================================
LAYER player_layer

LOCATE 1,23
INK col_gray
PRINT "score "
INK col_ltgray
STR.FMT score_fmt,"%07i",score
PRINT score_fmt

LOCATE 28,23
INK col_gray
PRINT "hi-score "
INK col_ltgray
STR.FMT hiscore_fmt,"%07i",hiscore
PRINT hiscore_fmt
RET

#================================================
    draw_player:
#================================================
LAYER player_layer
TILE.NEW ch_player,col_white,0
LOCATE player_x,player_y
PUT
RET

#================================================
    move_player:
#================================================
IF.EQ player_mv_ctr,0
    ADD player_x,player_x,player_dx
    IF.LT player_x,0
        VAR player_x,0
    ENDIF
    IF.GT player_x,44
        VAR player_x,44
    ENDIF
ENDIF
INC player_mv_ctr
IF.GT player_mv_ctr,player_mv_ctr_max
    VAR player_mv_ctr,0
ENDIF
RET

#================================================
    player_shoot:
#================================================
IF.EQ player_is_shooting,1
    RET
ENDIF
PLAY "L10O4CDF"
VAR player_is_shooting,1
VAR player_missile_x,player_x
VAR player_missile_y,player_y
DEC player_missile_y
RET

#================================================
    draw_player_missile:
#================================================
LAYER player_layer
TILE.NEW ch_missile,col_yellow,0
LOCATE player_missile_x,player_missile_y
PUT
RET

#================================================
    move_player_missile:
#================================================
IF.EQ player_missile_mv_ctr,0
    DEC player_missile_y
    IF.LT player_missile_y,0
        VAR player_is_shooting,0
    ENDIF
ENDIF
INC player_missile_mv_ctr
IF.GT player_missile_mv_ctr,player_missile_mv_ctr_max
    VAR player_missile_mv_ctr,0
ENDIF
RET

#================================================
    check_player_missile_collision:
#================================================
LAYER enemy_layer
LOCATE player_missile_x,player_missile_y
GET
TILE.GETP prop,"type"
IF.EQ prop,"enemy"
    TILE.NEW ch_enemy_dead,col_red,0
    TILE.SETP "type","enemy_dead"
    PUT
    VSYNC
    PLAY "L10O6ECD"
    VAR player_is_shooting,0
    ADD score,score,10
    IF.GT score,hiscore
        VAR hiscore,score
    ENDIF
ENDIF
RET

#================================================
    init_palette:
#================================================
VAR i,0
DEF col_black,i
PAL i,&h000000
INC i
DEF col_white,i
PAL i,&hffffff
INC i
DEF col_gray,i
PAL i,&h404040
INC i
DEF col_ltgray,i
PAL i,&h909090
INC i
DEF col_blue1,i
PAL i,&h000040
INC i
DEF col_blue2,i
PAL i,&h000080
INC i
DEF col_blue3,i
PAL i,&h0000a0
INC i
DEF col_green,i
PAL i,&h00ff00
INC i
DEF col_red,i
PAL i,&hff0000
INC i
DEF col_yellow,i
PAL i,&hffff00
INC i
DEF col_orange,i
PAL i,&hff8000
INC i
RET

#================================================
    init_charset:
#================================================
VAR i,1
DEF ch_player,i
CHR i,0,&b00011000
CHR i,1,&b00011000
CHR i,2,&b00111100
CHR i,3,&b01011010
CHR i,4,&b11011011
CHR i,5,&b11111111
CHR i,6,&b00100100
CHR i,7,&b00000000
INC i
DEF ch_enemy_1,i
CHR i,0,&b00000000
CHR i,1,&b00111000
CHR i,2,&b01111100
CHR i,3,&b10101010
CHR i,4,&b01111100
CHR i,5,&b10000010
CHR i,6,&b01000100
CHR i,7,&b00000000
INC i
DEF ch_enemy_2,i
CHR i,0,&b00111000
CHR i,1,&b01111100
CHR i,2,&b11010110
CHR i,3,&b01111100
CHR i,4,&b10000010
CHR i,5,&b10000010
CHR i,6,&b00000000
CHR i,7,&b00000000
INC i
DEF ch_missile,i
CHR i,0,&b00011000
CHR i,1,&b00011000
CHR i,2,&b00011000
CHR i,3,&b00000000
CHR i,4,&b00011000
CHR i,5,&b00000000
CHR i,6,&b00011000
CHR i,7,&b00000000
INC i
DEF ch_enemy_dead,i
CHR i,0,&b00000000
CHR i,1,&b00000000
CHR i,2,&b01001000
CHR i,3,&b00110000
CHR i,4,&b00110000
CHR i,5,&b01001000
CHR i,6,&b00000000
CHR i,7,&b00000000
INC i
RET
